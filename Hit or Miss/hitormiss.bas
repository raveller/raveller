10 CLS:KEY OFF:SCREEN 0,1:WIDTH 40:LOCATE ,,0
20 HS=0:V$=CHR$(176):S$=CHR$(32)
30 L$=S$:FOR X=1 TO 22:L$=L$+S$:NEXT X
40 FOR X=1 TO 28
50 B$=B$+CHR$(43-((X<13)+(X>16))*36):NEXT X
60 M$="////////////MISS////////////"
70 H$="============HIT!============"
80 Q$=CHR$(127):P$=STRING$(4,219):N$=STRING$(4,32)
90 CX=INT(RND*25)+9:IF CX>20 AND CX<25 THEN 90
100 FL=-1:DR=-1:DC=-1:SC=0:RX=11
110 HC=CX:HR=RX:NP=21:LP=19:M=0
120 CLS
130 COLOR 3:LOCATE 6,1:PRINT "YOUR":PRINT "SCORE:":PRINT:PRINT SC
140 COLOR 10:LOCATE 13,1:PRINT "HIGH":PRINT "SCORE:":PRINT:PRINT HS
150 KO=3:FOR X=2 TO 20:LOCATE X,8:COLOR 5:PRINT V$;
160 IF X>9 AND X<13 THEN PRINT SPC(28);V$:GOTO 180
170 COLOR KO:PRINT B$;:COLOR 5:PRINT V$
180 IF X=12 THEN KO=2
190 NEXT X
200 COLOR 15:LOCATE 11,12:PRINT "PRESS ANY KEY TO BEGIN."
210 COLOR 7:LOCATE 23,17:PRINT "PADDLE KEYS:"
220 PRINT TAB(12);"B LEFT N = RIGHT";
230 GOSUB 1030
240 K$=INKEY$:IF K$="" THEN 240
250 LOCATE 11,12:PRINT L$
260 IF RND>.05 THEN 310
270 CH=42:IF M>300 AND RND>.5 THEN CH=43
280 XR=INT(RND*16)+3
290 IF XR>9 AND XR<13 THEN 280
300 LOCATE XR,INT(RND*21)+10:COLOR 7:PRINT CHR$(CH);
310 CX=CX+DC:RX=RX+DR
320 PE=SCREEN(RX,CX,0)
330 IF PE=43 OR PE=79 THEN SC=SC+20:M=M+1:GOSUB 2000
340 IF PE=42 THEN SOUND 550,1:SC=SC+500:GOSUB 2000
350 IF PE=43 THEN DC=-DC:GOTO 460
360 IF CX>8 AND CX<37 THEN 390
370 DC=-DC:SOUND 100,5
380 CX=CX+DC:PE=SCREEN(RX,CX,0)
390 IF RX>1 AND RX<21 AND PE<>219 THEN 460
400 DR=-DR:SOUND 90,5
410 IF RX<2 THEN RX=2:IF NOT FL THEN 570
420 IF RX>20 THEN RX=20:IF FL THEN 570
430 IF PE=219 THEN RX=RX+2*DR:GOTO 460
440 SC=SC+M:GOSUB 2000
450 IF RND>.7 THEN GOSUB 1000
460 LOCATE HR,HC:PRINT S$;
470 LOCATE RX,CX:COLOR 15:PRINT Q$:COLOR 7
480 HC=CX:HR=RX:IF RX=11 THEN 260
490 K$=INKEY$
500 NP=NP+((K$="B")-(K$="N"))*2
510 IF LP=NP THEN 260
520 IF NP<9 THEN NP=9
530 IF NP>33 THEN NP=33
540 LOCATE 11,LP:PRINT N$:LP=NP
550 LOCATE 11,NP:PRINT P$
560 GOTO 260
570 FOR DE=1 TO 45:SOUND 300,.2:SOUND 800,.4
580 LOCATE HR,HC:PRINT CHR$(INT(RND*2)+42)
590 NEXT DE:LOCATE 23,1
600 FOR DE=1 TO 10:FOR DL=1 TO 20:NEXT DL
610 PRINT:NEXT DE:PRINT "SORRY, YOU MISSED."
620 PRINT:PRINT "YOUR SCORE WAS";SC;"POINTS."
630 IF SC>HS THEN HS=SC:PRINT:PRINT "A NEW RECORD!"
640 PRINT:PRINT "THE HIGH SCORE IS";HS;"POINTS."
650 PRINT:PRINT:PRINT:PRINT " PLEASE SELECT:":PRINT
660 PRINT " <R>EPLAY":PRINT " <Q>UIT"
670 PRINT:PRINT:PRINT:PRINT
680 K$=INKEY$:IF K$="Q" THEN END
690 IF K$="R" THEN 90 ELSE 680
1000 SOUND 400,2:SOUND 800,3:FL=NOT FL:IF FL THEN 1030
1010 LOCATE 1,8:COLOR 5:PRINT V$;:COLOR 4:PRINT M$;:COLOR 5:PRINT V$
1020 LOCATE 21,8:PRINT V$;:COLOR 14:PRINT H$;:COLOR 5:PRINT V$:RETURN
1030 LOCATE 1,8:COLOR 5:PRINT V$;:COLOR 14:PRINT H$;:COLOR 5:PRINT V$
1040 LOCATE 21,8:PRINT V$;:COLOR 4:PRINT M$;:COLOR 5:PRINT V$:RETURN
2000 COLOR 3:LOCATE 9,1:PRINT SC;:RETURN